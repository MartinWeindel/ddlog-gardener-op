import json
import document

typedef Kind = KindConfig
               | KindComponent{typeName: string}

typedef SpecStatus = SpecStatus {
    valid: bool,
    errors: Vec<string>
}

output relation Spec(kind: Kind, name: string, valid: bool)


input relation Config(name: string, yaml: string)

output relation InternalConfig(name: string, doc: Document, status: SpecStatus)


InternalConfig(name, doc, status) :-
    Config(name, yaml),
    (var doc, var status) = match (doc_from_yaml_string(yaml)) {
        Ok{doc} -> (doc, makeSpecStatusValid()),
        Err{e} -> (doc_unit(), makeSpecStatusInvalid(e))
    }.

Spec(KindConfig, name, status.valid) :- InternalConfig(name, _, status).


typedef Import = Import {
    name: string,
    source: Source
}

typedef Source = SourceConfig { name: string, select: string }
                 | SourceComponentByName { name: string, select: string }
                 | SourceComponentByType { compType: string, select: string }
                 | SourceComponentByTypeAndName { compType: string, name: string, select: string }

typedef Export = Export {
    name: string
}


input relation Component(name: string, typeName: string, spec: string)

output relation InternalComponent(name: string, kind: Kind, 
    imports: Map<string,Import>, exports: Map<string,Export>, status: SpecStatus)

InternalComponent(name, KindComponent{typeName}, imports, exports, status) :- 
    Component(name, typeName, spec),
    (var imports, var exports, var status) = parseComponentSpec(spec).

Spec(kind, name, status.valid) :- InternalComponent(name, kind, _, _, status).

output relation LevelTmp(name: string, level: u32)
output relation Level(name: string, level: u32)
output relation Dependency(from: string, to: string)

LevelTmp(name, 0) :- Config(name, _).
LevelTmp(name, 0) :- Component(name, _, _).

output relation CompImportTmp(name: string, imp: Import)

output relation CompImportDetail(name: string, importName: string, result: Result<Document,string>)
output relation InternalCompImports(name: string, doc: Document)

output relation CompImportCount(name: string, ok: bool, count: bit<64>)
output relation CompImportReady(name: string)

input relation CompExports(name: string, yaml: string)
output relation InternalCompExports(name: string, result: Result<Document,string>)

InternalCompExports(name, result) :-
    CompExports(name, yaml),
    var result = doc_from_yaml_string(yaml).

CompImportTmp(name, imp) :- 
    InternalComponent(name, _, imports, _, status), 
    status.valid, 
    var entry = FlatMap(imports),
    (var key, var imp) = entry.

CompImportDetail(name, imp.name, result) :-
    CompImportTmp(name, imp),
    SourceConfig{.name = var configName, .select = var select} = imp.source,
    InternalConfig(configName, doc, status),
    status.valid,
    var result = doc_select(doc, select).

CompImportDetail(name, imp.name, result) :-
    CompImportTmp(name, imp),
    SourceComponentByName{.name = var compName, .select = var select} = imp.source,
    InternalCompExports(compName, exportResult),
    Ok{var doc} = exportResult,
    var result = doc_select(doc, select).

Dependency(from, to) :-
    CompImportTmp(from, imp),
    SourceConfig{.name = var to, .select = var select} = imp.source.

Dependency(from, to) :-
    CompImportTmp(from, imp),
    SourceComponentByName{.name = var to, .select = var select} = imp.source.

LevelTmp(name, l + 1) :-
    Dependency(name, to),
    LevelTmp(to, l).

Level(name, level) :-
    LevelTmp(name, l),
    var level = Aggregate((name), group_max(l)).

CompImportCount(name, false, count) :-
    InternalComponent(name, _, imports, _, status),
    status.valid, 
    CompImportTmp(name, imp),
    var count = Aggregate((name), group_count(imp)).

CompImportCount(name, true, count) :- 
    InternalComponent(name, _, imports, _, status),
    status.valid, 
    CompImportDetail(name, imp, Ok{value}),
    not doc_is_unit(value),
    var count = Aggregate((name), group_count(imp)).

CompImportReady(name) :-
    CompImportCount(name, true, readyCount),
    CompImportCount(name, false, totalCount),
    readyCount == totalCount.

InternalCompImports(name, doc) :-
    CompImportReady(name),
    CompImportDetail(name, key, Ok{value}),
    var m = Aggregate((name), group2map((key, value))),
    var doc = doc_from_map(m).


function makeSpecStatusValid() : SpecStatus =
{
    var noErrs : Vec<string> = vec_empty();
    SpecStatus{true,noErrs}
}

function makeSpecStatusInvalid(e: string) : SpecStatus =
{
    SpecStatus{false,vec_singleton(e)}
}

function parseComponentSpec(yaml: string) : (Map<string,Import>, Map<string,Export>, SpecStatus) = 
{
    var imports: Map<string,Import> = map_empty();
    var exports: Map<string,Export> = map_empty();
    var errs : Vec<string> = vec_empty();
    var doc = doc_unit();

    match (doc_from_yaml_string(yaml)) {
        Ok{d} -> doc = d,
        Err{e} ->
          vec_push(errs, e);
          return (imports, exports, SpecStatus{false,errs})
    };

    match (doc_select(doc, ".imports")) {
        Ok{d} ->
          match (doc_as_map(d)) {
              Some{map} ->
                (var imps, var importsErrs) = parseComponentImports(map);
                imports = imps;
                vec_append(errs, importsErrs),
              None ->
                if (not doc_is_unit(d)) {
                    vec_push(errs, "Imports must be a map");
                    return (imports, exports, SpecStatus{false,errs})
                } 
          },
        Err{e} ->
          return (imports, exports, SpecStatus{false,errs})
    };

    match (doc_select(doc, ".exports")) {
        Ok{d} ->
          match (doc_as_map(d)) {
              Some{map} ->
                (var exps, var exportsErrs) = parseComponentExports(map);
                exports = exps;
                vec_append(errs, exportsErrs),
              None ->
                if (not doc_is_unit(d)) {
                    vec_push(errs, "Exports must be a map");
                    return (imports, exports, SpecStatus{false,errs})
                } 
          },
        Err{e} ->
          return (imports, exports, SpecStatus{false,errs})
    };

    (imports, exports, SpecStatus{vec_is_empty(errs), errs})
}

function parseComponentImports(map: Map<Document,Document>) : (Map<string,Import>, Vec<string>) =
{
    var imports: Map<string,Import> = map_empty();
    var errs : Vec<string> = vec_empty();

    for (entry in map) {
        (var k, var v) = entry;
        match (doc_as_string(k)) {
            Some{key} ->
                match (doc_select(v, ".sourceType")) {
                    Ok{st} ->
                        match (doc_as_string(st)) {
                            Some{"config"} ->
                                match (parseSourceTypeConfig(key, v)) {
                                    Ok{imp} -> map_insert(imports, key, imp),
                                    Err{e} -> vec_push(errs, e)
                                },
                            Some{"component"} ->
                                match (parseSourceTypeComponent(key, v)) {
                                    Ok{imp} -> map_insert(imports, key, imp),
                                    Err{e} -> vec_push(errs, e)
                                },
                            Some{x} ->
                                vec_push(errs, "imports: invalid sourceType ${x} for key ${key}"),
                            None ->
                                vec_push(errs, "imports: invalid sourceType value for key ${key}")
                        },
                    Err{e} ->
                        vec_push(errs, "imports: select sourceType failed with ${e}")
                },
            None ->
                vec_push(errs, "imports: invalid key:${k}")
        }
    };
    (imports, errs)
}

function parseSourceTypeConfig(key: string, value: Document) : Result<Import, string> = 
{
    var name = "";
    var select = "";
    match (extractStringField(value, "name")) {
        Ok{s} -> name = s,
        Err{e} -> return Err{"imports: key ${key}: ${e}"}
    };
    match (extractStringField(value, "select")) {
        Ok{s} -> select = s,
        Err{e} -> return Err{"imports: key ${key}: ${e}"}
    };
    Ok{Import{key, SourceConfig{name, select}}}
}

function parseSourceTypeComponent(key: string, value: Document) : Result<Import, string> = 
{
    var name : Option<string> = None;
    var typeName : Option<string> = None;
    var select = "";
    match (extractOptionalStringField(value, "name")) {
        Ok{s} -> name = s,
        Err{e} -> return Err{"imports: key ${key}: ${e}"}
    };
    match (extractOptionalStringField(value, "type")) {
        Ok{s} -> typeName = s,
        Err{e} -> return Err{"imports: key ${key}: ${e}"}
    };
    match (extractStringField(value, "select")) {
        Ok{s} -> select = s,
        Err{e} -> return Err{"imports: key ${key}: ${e}"}
    };
    match (typeName) {
        Some{s} ->
            match (name) {
                Some{s2} ->
                    Ok{Import{key, SourceComponentByTypeAndName{s, s2, select}}},
                None ->
                    Ok{Import{key, SourceComponentByType{s, select}}}
            },
        None ->
            match (name) {
                Some{s} ->
                    Ok{Import{key, SourceComponentByName{s, select}}},
                None ->
                    Err{"imports: key ${key}: either name or type must be set"}
            }
    }
}

function parseComponentExports(map: Map<Document,Document>) : (Map<string,Export>, Vec<string>) =
{
    var exports: Map<string,Export> = map_empty();
    var errs : Vec<string> = vec_empty();

    for (entry in map) {
        (var k, var v) = entry;
        match (doc_as_string(k)) {
            Some{key} ->
                map_insert(exports, key, Export{key}),
            None ->
                vec_push(errs, "exports: invalid key:${k}")
        }
    };
    (exports, errs)
}

function extractStringField(value: Document, key: string) : Result<string, string> = 
{
    match (doc_select(value, ".${key}")) {
        Ok{d} ->
            match (doc_as_string(d)) {
                Some{s} ->
                    Ok{s},
                None ->
                    Err{"${key} value must be a string"}
            },
        Err{e} ->
            Err{"${key} field not found"}
    }
}

function extractOptionalStringField(value: Document, key: string) : Result<Option<string>, string> = 
{
    match (doc_select(value, ".${key}")) {
        Ok{d} ->
            match (doc_as_string(d)) {
                Some{s} ->
                    Ok{Some{s}},
                None ->
                    if (not doc_is_unit(d)) {
                        return Err{"optional ${key} value must be a string"}
                    };
                    Ok{None}
            },
        Err{e} ->
            Err{"${key} field not found"}
    }
}
